apply plugin: "com.jfrog.bintray"

ext.sharedManifest = manifest {
    attributes 'Implementation-Vendor': group,
            'Implementation-Title': name,
            'Implementation-Version': version
}

jar {
    manifest {
        from sharedManifest
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest {
        from sharedManifest
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
        }
    }
}

bintray {
    user = findProperty('bintray_api_user')
    key = findProperty('bintray_api_key')

    publications = ['mavenJava']

    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        userOrg = 'ainrif'
        name = rootProject.name
        websiteUrl = 'https://bitbucket.org/ainrif/apiator'
        issueTrackerUrl = 'https://bitbucket.org/ainrif/apiator/issues'
        vcsUrl = 'https://bitbucket.org/ainrif/apiator.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

bintrayUpload {
    onlyIf {
        versionHolder.release
    }
}

task publishGitTag {
    onlyIf {
        versionHolder.release
    }
    doLast {
        versionHolder.git.with {
            tag.add(name: project.version, annotate: false, force: true)
            push(all: false)
            push(tags: true)
        }
    }
}

publish.dependsOn bintrayUpload
publish.dependsOn publishGitTag