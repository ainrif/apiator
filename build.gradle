import java.time.Instant

buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:2.2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply from: "$rootProject.projectDir/gradle/versions.gradle"

ext.versions = [
        slf4j    : '1.7.26',
        groovy   : '2.5.7',
        lang3    : '3.9',
        jackson  : '2.9.9',
        jsr305   : '3.0.2',
        micronaut: '1.1.+',

        spock    : '1.3-groovy-2.5',
        cglib    : '3.2.12',
        logback  : '1.2.3'
]

allprojects {
    apply plugin: 'groovy'

    group = 'com.ainrif.apiator'
    version = versionHolder.version

    task publishRelease {
        onlyIf {
            versionHolder.release
        }
    }

    repositories {
        mavenLocal()
        jcenter()

        maven { url "http://dl.bintray.com/ainrif/maven" }
    }

    configurations {
        all {
            it.exclude group: 'commons-logging'
            it.exclude group: 'org.apache.logging.log4j'
            it.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        }
    }

    dependencies {
        compileOnly "org.slf4j:jcl-over-slf4j:${versions.slf4j}"
        compileOnly "org.slf4j:log4j-over-slf4j:${versions.slf4j}"

        testCompile "org.spockframework:spock-core:${versions.spock}"

        testCompile "cglib:cglib:${versions.cglib}"
        testCompile "org.apache.commons:commons-lang3:${versions.lang3}"

        testCompile "org.slf4j:jcl-over-slf4j:${versions.slf4j}"
        testCompile "org.slf4j:log4j-over-slf4j:${versions.slf4j}"
        testCompile "ch.qos.logback:logback-classic:${versions.logback}"
    }
}

subprojects {
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    def archiveReportsTask = task archiveReports(type: Zip) {
        from 'build/reports/tests'
        appendix = 'tests_reports'
        version = "${project.version}_${Instant.now()}"
        onlyIf {
            test.state.failure
        }
    }

    test {
        finalizedBy archiveReportsTask
        testLogging {
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }
}

dependencies {
    compile project(':apiator-all')

    compile project(':test:core-model-test')
    compile project(':test:jax-rs-model-test')
    compile project(':test:micronaut-model-test')
    compile project(':test:demo-model-test')
}

apply from: "$rootProject.projectDir/gradle/uiDev.gradle"

task publishGitTag {
    onlyIf {
        versionHolder.release
    }
    doLast {
        versionHolder.git.with {
            try {
                tag.add(name: project.version, annotate: false, force: true)
            } catch (Exception e) {
                // jGit doesn't process force flag properly at org.eclipse.jgit.api.TagCommand#updateTagRef:184
                if (e.class.simpleName != 'JGitInternalException' || !e.message.contains('NO_CHANGE')) {
                    throw e
                }
            }
            push(all: false)
            push(tags: true)
        }
    }
}

publishRelease.dependsOn publishGitTag